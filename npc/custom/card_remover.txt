//===== rAthena Script =======================================
//= Card Removal NPC
//===== By: ==================================================
//= TyrNemesis^
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================

prontera,147,175,4	script	Removedora de Cartas#eAcustom	78,{

	set .zenycost,200000;    // base cost of the card remover services (in Zeny)
	set .percardcost,25000;  // cost per card of the card remover services (in Zeny)
	set .faildestroy,1;      // should the card remover have a chance of failure that destroys items? (1=yes, 0=no)

	disable_items;
	mes "^FF8C00[Removedora de Cartas]^000000";
	mes "Bom dia, jovem. Eu tenho o poder de remover cartas que você compôs em seu equipamento. Essa ideia te agrada?";
	next;
	switch(select("^0000CD»^000000 Sim.:^0000CD»^000000 O que você cobra?:^FF0000»^000000 Não, Obrigado.")) {
	case 1:
		mes "^FF8C00[Removedora de Cartas]^000000";
		mes "Muito bem. Qual item devo examinar para você ?";
		next;

		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
			if( getequipisequiped(.@indices[.@i]) )
				set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@menu$, .@menu$ + ":";
		}
		set .@part, .@indices[ select(.@menu$) ];
		if(!getequipisequiped(.@part)) {
			mes "^FF8C00[Removedora de Cartas]^000000";
			mes "Jovem... Você não está usando nada aí que eu possa remover cartas.";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "^FF8C00[Removedora de Cartas]^000000";
			mes "Jovem... Não há cartas equipadas neste item. Não posso fazer nada com isso.";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFSó um minuto!^000000";
			mes "Eu não posso oferecer nenhum dos meus";
			mes "serviços para você porque";
			mes "você está carregando muita";
			mes "coisa. Coloque seus itens extras";
			mes "na Kafra, e volte mais tarde~";
			close;
		}
		mes "^FF8C00[Removedora de Cartas]^000000";
		mes "Este item tem " + .@cardcount + " cartas equipadas nele. Para realizar minha magia, precisarei " + (.zenycost+(.@cardcount * .percardcost)) + " zeny,^0000FF Fragmento Estelar^000000,e ^0000FF Gema Amarela^000000.";
		next;
		if(select("^0000CD»^000000 Muito bem. Faça isso.:^FF0000»^000000  Deixa para lá.") == 2) {
			mes "[Removedora de Cartas]";
			mes "Muito bem. Volte imediatamente se você quiser meus serviços.";
			close;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[Removedora de Cartas]";
			mes "Você não tem todos os itens que preciso para fazer minha mágica, jovem. Venha novamente quando você tiver todos os itens.";
			close;
		}
		mes "^FF8C00[Removedora de Cartas]^000000";
		mes "Antes de começar, devo avisá-lo: posso falhar. Se fizer isso, posso destruir as cartas, o item ou ambos. Eu não dou reembolsos. Dito isto, o que é mais importante para você: As cartas ou o item ?";
		next;
		switch(select("^FF0000»^000000  mudei de ideia sobre isso.:^0000CD»^000000 O item.:^0000CD»^000000 As cartas.")) {
		case 1:
			mes "^FF8C00[Removedora de Cartas]^000000";
			mes "Muito bem. Volte imediatamente quando mudar de ideia.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "^FF8C00[Removedora de Cartas]^000000";
		mes "Muito bem. vou começar.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1; //Fragmento Estrelar
		delitem 715,1; //Gema Amarela
		
		// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
		// First value = Total failure chance (item and cards destroyed)
		// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
		// Third value = Harmless failure chance (all that's lost is your investment)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "^FF8C00[Removedora de Cartas]^000000";
				mes "O processo foi um fracasso total. Receio que o item e as cartas tenham sido destruídos.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "^FF8C00[Removedora de Cartas]^000000";
					mes "Embora eu tenha conseguido remover as cartas do item, elas foram destruídas no processo. O item, no entanto, está bem.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "^FF8C00[Removedora de Cartas]^000000";
					mes "Muito infeliz. Consegui remover as cartas, mas o item em si foi destruído no processo.";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "^FF8C00[Removedora de Cartas]^000000";
			mes "Não consegui remover as cartas. Felizmente, tanto o item quanto as cartas ainda estão bem.";
			close;
		}
		next;
		successremovecards .@part;
		mes "^FF8C00[Removedora de Cartas]^000000";
		mes "O processo foi um sucesso. Aqui estão as suas cartas e seu item. Até a próxima.";
		close;
	case 2:
		mes "^FF8C00[Removedora de Cartas]^000000";
		mes "Cobro uma taxa fixa de "+callfunc("F_InsertComma",.zenycost)+" zeny, mais "+callfunc("F_InsertComma",.percardcost)+"zeny para cada carta que eu removo do item. Além disso, preciso de uma migalha de estrela e de uma pedra preciosa amarela para fazer minha mágica..";
		close;
	case 3:
		mes "^FF8C00[Removedora de Cartas]^000000";
		mes "Muito bem. Volte imediatamente quando tiver interesse nos meus serviços.";
		close;
	}
}
