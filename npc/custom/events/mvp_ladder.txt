//===== rAthena Script =======================================
//= MVP Ladder Game
//===== By: ==================================================
//= aftermath, AnnieRuru (rewrite)
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Gather a party and kill every MVP in ascending order.
//===== Additional Comments: =================================
//= 1.0 First version, edited. [Euphy]
//============================================================

prontera,136,171,3	script	O Melhor do MvP	450,{
		mes "[O Melhor do MvP]";
		mes "Você precisa pagar uma taxa para entrar.";
		mes "500 <ITEM>Celt Coin<INFO>42015</INFO></ITEM>.";
		next;
		mes "[O Melhor do MvP]";
		mes "O que deseja fazer?";
	next;
	switch(select("Sim! Bora pra dentro!:Informação.:Mostre-me o melhor.:Não.")) {
	case 1:
		if (countitem(42015) < 500) {
		mes "[O Melhor do MvP]";
		mes "Parece que você não tem 500 <ITEM>Celt Coin<INFO>42015</INFO></ITEM>s. necessarios!";
		close;
	}
		break;
	case 2:
		mes "[O Melhor do MvP]";
		mes "Neste jogo, sua equipe deve matar todos os monstros MvP em ordem ascendente, do mais fraco para o mais forte.";
		if (.finish_item_amount)
		mes "Se sua equipe conseguir completar o jogo da escada MvP, cada membro ganhará "+ callfunc("F_InsertPlural", .finish_item_amount, getitemname( .finish_item_id )) +".";
		if (.register_cost)
		mes "Mas a taxa de entrada é de "+ callfunc( "F_InsertComma", .register_cost ) +" zeny.";
		next;
		mes "[O Melhor do MvP]";
		mes "Você perde o jogo se não conseguir terminar em "+ .timeout +" minutos, ou se toda a sua equipe for derrotada.";
		mes "Boa sorte!";
		close;

case 3:
    mes "[O Melhor do MvP]";
    if (!$mvpladdderparty_time) {
        mes "Ninguém terminou este jogo ainda.";
        close;
    }
    mes "O melhor recorde é";
    mes "[ "+($mvpladdderparty_time / 60 )+" min "+($mvpladdderparty_time % 60 )+" seg ]";
    mes "Pela equipe ^FF0000"+$mvpladdderparty_name$+"^000000.";
    .@size = getarraysize($mvpladderparty_member$);
    for (.@i = 0; .@i < .@size; .@i++)
        mes "^000000"+( .@i +1 )+". ^0000FF"+$mvpladderparty_member$[.@i];
    if (getgmlevel() < .gmlvlreset) close;
    next;
    if (select("Fechar.", "Redefinir o recorde.") == 1) close;
    if (select("Não se preocupe.", "Eu realmente quero resetá-lo.") == 1) close;
    $mvpladdderparty_time = 0;
    $mvpladdderparty_name$ = "";
    deletearray $mvpladderparty_member$[.@i];
    mes "[O Melhor do MvP]";
    mes "Registro redefinido com sucesso.";
    close;
case 4:
    mes "[O Melhor do MvP]";
    mes "Quando você estiver forte o suficiente para completar o jogo, por favor, volte.";
    close;
}
if (!getcharid(1)) {
    mes "[O Melhor do MvP]";
    mes "Você precisa formar uma equipe para jogar.";
    close;
}
if (is_party_leader() == false) {
    mes "[O Melhor do MvP]";
    mes "Apenas o líder da equipe pode se registrar.";
    close;
}

	.@origin = getcharid(3);
	getpartymember getcharid(1), 1;
	getpartymember getcharid(1), 2;
	for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
		if ( isloggedin( $@partymemberaid[.@i], $@partymembercid[.@i] ) ) {
			attachrid $@partymemberaid[.@i];
			if ( strcharinfo(3) == strnpcinfo(4) )
				.@online++;
		}
	}
	attachrid .@origin;
	if ( $@partymembercount != .register_min ) {
		mes "[O Melhor do MvP]";
		mes "Você precisa formar uma equipe com exatamente "+ .register_min +" membros para jogar.";
		close;
	}
	else if ( .@online != .register_min )  {
		mes "[O Melhor do MvP]";
		mes "Sua equipe deve ter "+ .register_min +" membros online no mapa '"+ strnpcinfo(4) +"'.";
		close;
	}
	else if ( .register_cost && Zeny < .register_cost ) {
		mes "[O Melhor do MvP]";
		mes "You don't have enough zeny. Please come back when you do.";
		close;
	}
	else if ( .party_id ) {
		mes "[O Melhor do MvP]";
		mes "Desculpe, mas uma equipe está atualmente jogando o jogo. Por favor, aguarde até que a equipe termine.";
		mes "Obrigado";
		close;
	}

	Zeny -= .register_cost;
	announce "O grupo ["+ strcharinfo(1) +"] iniciou o O Melhor do MvP.", bc_all;
	delitem 42015,500;
	set .party_id, getcharid(1);
	set .@time_enter, gettimetick(2);
	for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
		if ( isloggedin( $@partymemberaid[.@i], $@partymembercid[.@i] ) ) {
			attachrid $@partymemberaid[.@i];
			if ( strcharinfo(3) == strnpcinfo(4) ) {
				announce "Voce tem "+ .timeout +" minutos para completar "+ .totalround +" rounds.", bc_self;
				.@name$[.@c] = strcharinfo(0);
				.@c++;
			}
		}
	}
	cleanmap .eventmap$;
	warpparty .eventmap$, 0,0, .party_id, strnpcinfo(4);
	donpcevent strnpcinfo(0)+"::OnMvpDead";
	sleep .timeout * 60000;
	if ( .round == .totalround +1 ) {
		getpartymember .party_id, 1;
		getpartymember .party_id, 2;
		mapannounce .eventmap$, "Parabéns... Você conseguiu derrotar todos os MVPs!", bc_map;
		for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
			if ( isloggedin( $@partymemberaid[.@i], $@partymembercid[.@i] ) ) {
				attachrid $@partymemberaid[.@i];
				if ( strcharinfo(3) == .eventmap$ )
					getitem .finish_item_id, .finish_item_amount;
			}
		}
		set .@timeused, gettimetick(2) - .@time_enter;
		if ( .bonus_item_amount && .@timeused < .bonus_time * 60 ) {
			mapannounce .eventmap$, "Você é recompensado com um item bônus por completar a escada dentro de "+ .bonus_time +" minutos.", bc_map;

			for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
				if ( isloggedin( $@partymemberaid[.@i], $@partymembercid[.@i] ) ) {
					attachrid $@partymemberaid[.@i];
					if ( strcharinfo(3) == .eventmap$ )
						getitem .bonus_item_id, .bonus_item_amount;
				}
			}
		}

		if ( !$mvpladdderparty_time || .@timeused < $mvpladdderparty_time ) {
			mapannounce .eventmap$, "E você quebrou o recorde! [ "+( .@timeused / 60 )+" min "+( .@timeused % 60 )+" seg ]", bc_map;

			set $mvpladdderparty_time, .@timeused;
			set $mvpladdderparty_name$, getpartyname( .party_id );
			copyarray $mvpladderparty_member$, .@name$, .register_min;
		}
		else
			mapannounce .eventmap$, "Tempo utilizado [ "+( .@timeused / 60 )+" min "+( .@timeused % 60 )+" seg ]", bc_map;

		sleep 10000;
		announce "A equipe ["+ getpartyname( .party_id ) +"] concluiu o jogo Melhor do MvP!", bc_all;

	}
	else
		announce "A equipe ["+ getpartyname( .party_id ) +"] falhou em concluir o jogo Melhor do MvP.", bc_all;

	mapwarp .eventmap$, .map$, .x, .y;
	killmonsterall .eventmap$;
	.party_id = .round = 0;
	end;

OnMvpDead:
	getpartymember .party_id, 1;
	getpartymember .party_id, 2;
	.round++;
	if ( .round >= 2 && .round != .totalround +1 && .round_item_amount ) {
		for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
			if ( isloggedin( $@partymemberaid[.@i], $@partymembercid[.@i] ) ) {
				attachrid $@partymemberaid[.@i];
				if ( strcharinfo(3) == .eventmap$ )
					getitem .round_item_id, .round_item_amount;
			}
		}
	}
	if ( .round == .totalround +1 ) {
		awake strnpcinfo(0);
		end;
	}
	else if ( .round == .totalround )
    mapannounce .eventmap$, "A Rodada Final começará em "+ .delay +" segundos...", bc_map;
else
    mapannounce .eventmap$, "Iniciando rodada "+ .round +" em "+ .delay +" segundos...", bc_map;
	sleep .delay * 1000;
	if ( .mvpid[.round] == 1646 )  // pick random Bio3 MVP
		.mvpid[.round] = rand(1646,1651);
	monster .eventmap$,0,0, "--ja--", .mvpid[.round], 1, strnpcinfo(0)+"::OnMvpDead";
	mapannounce .eventmap$, getmonsterinfo( .mvpid[.round], MOB_NAME ) +" apareceu!", bc_map|bc_blue;

	end;

OnPCLogoutEvent:
	if ( hp > 0 )
		.@less_one = 1;
	else
		end;
OnPCDieEvent:
	if ( strcharinfo(3) != .eventmap$ || !getcharid(1) ) end;
	if ( getcharid(1) != .party_id ) end;
	getpartymember .party_id, 1;
	getpartymember .party_id, 2;
	for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
		if ( isloggedin( $@partymemberaid[.@i], $@partymembercid[.@i] ) ) {
			attachrid $@partymemberaid[.@i];
			if ( strcharinfo(3) == .eventmap$ && hp > 0 )
				.@alive++;
		}
	}
	if ( .@less_one )
		.@alive--;
	if ( !.@alive ) {
		mapannounce .eventmap$, "Grupo miou!", bc_map;
		sleep 10000;
		awake strnpcinfo(0);
	}
	end;

OnInit:
//	Configurations -----------------------------------------------------

	// Time limit (in minutes)
	// When time runs out, all players inside the room will be kicked out.
	// Do NOT set this to zero!
	set .timeout, 60;

	// entrance fee (in Zeny)
	set .register_cost, 0;

	// exact amount of party members needed to start the game
	set .register_min, 2;

	// id of each mvp. you can add more
	setarray .mvpid[1],
		1086,//	Golden Thief Bug	64
		1115,//	Eddga				65
		1150,//	Moonlight Flower	67
		1159,//	Phreeoni			69
		1112,//	Drake				70
		1583,//	Tao Gunka			70
		1492,//	Incantation Samurai	71
		1046,//	Doppelgangger		72
		1252,//	Garm				73
		1418,//	Evil Snake Lord		73
		1059,//	Mistress			74
		1190,//	Orc Lord			74
		1087,//	Orc Hero			77
		1251,//	Knight of Windstorm	77
		1038,//	Osiris				78
		1658,//	Ygnizem				79
		1272,//	Dark Lord			80
		1871,//	Fallen Bishop		80
		1039,//	Baphomet			81
		1147,//	Maya				81
		1785,//	Atroce				82
		1389,//	Dracula				85
		1630,//	Bacsojin			85
		1885,//	Gorynych			85
		1623,//	RSX 0806			86
		1511,//	Amon Ra				88
		1688,//	Lady Tanee			89
		1768,//	Gloom Under Night	89
		1719,//	Datale				90
		1734,//	Kiel D-01			90
		1157,//	Pharaoh				93
		1373,//	Lord of Death		94
		1312,//	Turtle General		97
		1779,//	Ktullanux			98
		1874,//	Beelzebub			98
		1646,// Bio3 placeholder    99
		1708,//	Thanatos			99
		1751,//	Valkyrie Randgris	99
		1832,//	Ifrit				99
		2156,//	Leak				99
		1685,//	Vesper				99
		1917,//	Satan				99
		2022,//	Nidhoggr's			99
		2202,//	Kraken				99
		2475,//	Root of Corruption	99
		2087,//	Queen Scaraba		99
		2476,//	Amdarais			99
		2255,//	Kades				99
		2362,//	Amon Ha Nightmare	99
		31002;// Belial	99
	// number of rounds (default: 39)
	set .totalround, getarraysize(.mvpid) -1;

	// item reward for completing each round
	set .round_item_id, 607;
	set .round_item_amount, 1;

	// item reward for completing the entire ladder
	set .finish_item_id, 42015;
	set .finish_item_amount, 1000;

	// bonus reward if ladder completed within a certain time (in minutes)
	set .bonus_time, 30; // if completed within 45 minutes, this reward is given
	set .bonus_item_id, 42015;
	set .bonus_item_amount, 500;

	// time delay between rounds, in seconds (default: 3)
	set .delay, 5;

	// minimum GM level to reset the best record
	set .gmlvlreset, 99;

	// event map
	set .eventmap$, "guild_vs2-2";

	// mapflag configuration
	setarray .@mapflag,
		mf_nowarp,
		mf_nowarpto,
		mf_nosave,
		mf_nomemo,
		mf_noteleport,
		mf_nopenalty, // disable exp loss
		mf_noreturn,
		mf_nobranch,
		mf_nomobloot, // disable monster drop loots,
		mf_nomvploot, // 2 of these
		mf_partylock;

//	Config Ends --------------------------------------------------------------

	mapannounce .eventmap$, "An administrator has refreshed the server. Please re-register. We apologize for the inconvenience.", bc_map;
	getmapxy .map$, .x, .y, BL_NPC;
	mapwarp .eventmap$, .map$, .x, .y;
	killmonsterall .eventmap$;
	.@size = getarraysize( .@mapflag );
	for ( .@i = 0; .@i < .@size; .@i++ )
		setmapflag .eventmap$, .@mapflag[.@i];
	end;
}
