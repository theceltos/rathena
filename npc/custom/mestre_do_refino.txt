//===== rAthena Script ======================================= 
//= Ticket Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses +5~9/+11 refine tickets to refine
//= equipment with no chance of failure.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Do not refine above ticket level. [Euphy]
//============================================================
//
// barcellos 21/02/2024
// millu@barcellosjorge.net.br
// alterado 
// modelo de array para os tickets
//129	.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );	
//130	switch(.@itemtype) {

// Main NPC :: safety_Ref_NPC
//============================================================
prontera,184,177,6	script	Refine Master	851,{
	disable_items;
	/* setarray .@cert_weapon[0],
		6864,	// Guarantee_Weapon_19Up
		6875,	// Guarantee_Weapon_18Up
		6874,	// Guarantee_Weapon_17Up
		6873,	// Guarantee_Weapon_16Up
		6872,	// Guarantee_Weapon_15Up
		6871,	// Guarantee_Weapon_14Up
		6870,	// Guarantee_Weapon_13Up
		6584,	// Guarantee_Weapon_12Up
		6238,	// Guarantee_Weapon_11Up
		6228,	// Guarantee_Weapon_9Up
		6229,	// Guarantee_Weapon_8Up
		6230,	// Guarantee_Weapon_7Up
		6231,	// Guarantee_Weapon_6Up
		6456;	// Guarantee_Weapon_5Up
		*/
	if ( countitem(6864) || countitem(6875) || countitem(6874) || 
		countitem(6873) || countitem(6872) || countitem(6871) || 
		countitem(6870) || countitem(6584) || countitem(6238) || 
		countitem(6228) || countitem(6229) || countitem(6230) || 
		countitem(6231) || countitem(6456))
		set .@bWeaponUp,1;
/*
	setarray .@cert_armor[0],
		6865,	// Guarantee_Armor_19Up
		6881,	// Guarantee_Armor_18Up
		6880,	// Guarantee_Armor_17Up
		6879,	// Guarantee_Armor_16Up
		6878,	// Guarantee_Armor_15Up
		6877,	// Guarantee_Armor_14Up
		6876,	// Guarantee_Armor_13Up
		6585,	// Guarantee_Armor_12Up
		6239,	// Guarantee_Armor_11Up
		6232,	// Guarantee_Armor_9Up
		6233,	// Guarantee_Armor_8Up
		6234,	// Guarantee_Armor_7Up
		6235,	// Guarantee_Armor_6Up
		6457;	// Guarantee_Armor_5Up
*/
	if (	countitem(6865) || countitem(6881) || countitem(6880) || 
		countitem(6879) || countitem(6878) || countitem(6877) || 
		countitem(6876) || countitem(6585) || countitem(6239) || 
		countitem(6232) || countitem(6233) || countitem(6234) || 
		countitem(6235) || countitem(6457))
		set .@bArmorUp,1;
	if (!.@bWeaponUp && !.@bArmorUp) {
		mes "[Refine Master]";
		mes "Olá!";
		mes "Tudo bem?";
		mes "Eu sou especialista em";
		mes "refinar itens";
		mes "apesar de estar sem trabalho a algum tempo";
		next;
		switch(select("Não estou interessado.:Hmm... fiquei curioso.")) {
		case 1:
			mes "[Refine Master]";
			mes "Tenha cuidado aventureiro.";
			close;
		case 2:
			mes "[Refine Master]";
			mes "Na verdade, forneço serviços de refinamento para aventureiros com um ^FF0000Refine Ticket^000000...";
			mes "Até breve!";
			close;
		}
	}
	emotion ET_SURPRISE;
	mes "[Refine Master]";
	mes "Saudações";
	mes "Eu posso refinar um equipamento para o mesmo nível de um ticket.";
	mes "Você não precisa se preocupar! Não há chance de quebrar seu item.";
	next;
	if(select("Vou buscar um ticket.:Refine agora, por favor.") == 1) {
		mes "[Refine Master]";
		mes "Ok.";
		mes "Volte quando estiver pronto.";
		close;
	}
	mes "[Refine Master]";
	mes "Qual equipamento quer refinar?";
	next;
	setarray .@position$[1],"Head upper","Armor","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head middle","Head lower";
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$+((getequipisequiped(.@indices[.@i]))?getequipname(.@indices[.@i]):.@position$[.@i]+"- [Empty]")+":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[Refine Master]";
		mes "Você deve equipar o equipamento que deseja refinar";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion ET_OTL;
		mes "[Refine Master]";
		mes "Desculpe,";
		mes "mas este item não é refinável.";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );	
	switch(.@itemtype) {
	default:
	case 4:	//armor
		setarray .@levels[0],19,18,17,16,15,14,13,12,11,9,8,7,6,5;		
		setarray .@tickets[0],6865,6881,6880,6879,6878,6877,6876,6585,6239,6232,6233,6234,6235,6457;
		set .@type$,"Armor";
		set .@check,.@bArmorUp;
		break;
	case 5:
		setarray .@levels[0],19,18,17,16,15,14,13,12,11,9,8,7,6,5;
		setarray .@tickets[0],6864,6875,6874,6873,6872,6871,6870,6584,6238,6228,6229,6230,6231,6456;
		
		set .@type$,"Weapon";
		set .@check,.@bWeaponUp;
		break;
	}
	if (!.@check) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "Se você quer refinar ^FF0000"+.@type$+"^000000, precisa ter um ticket do tipo ^B84728"+.@type$+" ^000000.";
		mes "Volte com o ticket apropriado!";
		close;
	}
	mes "[Refine Master]";
	mes "Selecione o Ticket de Refino a utilizar para ^FF0000"+.@type$+" ^000000.";
	next;
	set .@menu$,"";
	for(set .@i,0; .@i<getarraysize(.@tickets); set .@i,.@i+1){
		//if(countitem(.@tickets[.@i])>0){
			set .@menu$, .@menu$+getitemname(.@tickets[.@i])+":";
		//}
	}
	set .@select, select(.@menu$)-1;
	set .@ticket_lv, .@levels[.@select];
	set .@ticket_id, .@tickets[.@select];
	if (countitem(.@ticket_id) == 0) {
		emotion ET_QUESTION;
		mes "[Refine Master]";
		mes getitemname(.@ticket_id)+" não está no inventário. Você o colocou no inventário?";
		mes "Verifique isto";
		mes "e retorne mais tarde!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[Refine Master]";
		mes "^FF0000Este item já está refinado ao nível do ticket.^000000";
		mes "Por favor, tenha um ticket de refino de nível maior que seu equipamento";
		close;
	}
	mes "[Refine Master]";
	mes "Vou refinar ^FF0000"+getequipname(.@part)+"^FF0000 até o nível +"+.@ticket_lv+"^000000 com ^FF0000"+getitemname(.@ticket_id)+"^000000 .";
	mes "Vamos continuar?";
	next;
	if(select("Não.:Sim.") == 1) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "Ah... mudou de ideia?";
		mes "Ok.";
		mes "Volte quando acabar sua indecisão.";
		close;
	}
	mes "[Refine Master]";
	mes "Grande!!!";
	mes "Como você desejou!";
	mes "Eu sou bom no que faço, não...";
	mes ".......ka boom!";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id)) {
		delitem .@ticket_id,1;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) ||
			callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
			mes "[Refine Master]";
			emotion ET_FRET;
			mes "Só um segundo...";
			mes "Está pensando que sou estúpido?!";
			mes "Você trocou o item enquanto eu não estava olhando! Saia daqui!";
			close;
		}
	} else {
		next;
		mes "Error!";
		mes "Informe ao Admin.";
		close;
	}
	successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
	next;
	emotion ET_DELIGHT;
	mes "[Refine Master]";
	mes "Beleza... aqui está~";
	mes "Bem, ^0000FF"+strcharinfo(0)+"^000000!";
	mes "Parabéns por sua conquista "+.@type$+".";
	mes "Siga em frente!";
	mes "Até breve~!";
	close;
}