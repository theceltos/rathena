//===== rAthena Script =======================================
//= Reset NPC
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//= 1.5 Added sc_end_class to reset related status changes [sader1992]
//============================================================
prontera,149,194,4	script	Garota do Reset	124,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	5000,	5000,	9000,	0;
	mes "[Garota de Reset]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "Sorry you can only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life.";
		close;
	}
	mes "Eu sou a garota do reset.";
	mes "Resetar Atributos: "+ callfunc("F_InsertComma",.@Reset[1]) +"z";
	mes "Resetar Habilidades: "+ callfunc("F_InsertComma",.@Reset[0]) +"z";
	mes "Resetar Tudo: "+ callfunc("F_InsertComma",.@Reset[2]) +"z";
	if(.@Reset[3]) mes "You may only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+", so use "+((.@Reset[3]>1)?"them":"it")+" wisely.";
	mes "Please select the service you want:";
	next;
	set .@i,(select("^FF3355Resetar Habilidades:Resetar Atributos:Resetar Tudo^000000:Cancel"));
	if(.@i > 3) close;
	mes "[Garota de Reset]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "Você não tem Zeny.";
		close;
	}
	if(.@Reset[3]){
		mes "You can only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life, are you sure?";
		if(select("Let me think:That's fine") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "E lá vamos nós!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
